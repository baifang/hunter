# Copyright (c) 2013, Ruslan Baratov
# All rights reserved.

if(DEFINED HUNTER_CMAKE_HUNTER_)
  return()
else()
  set(HUNTER_CMAKE_HUNTER_ 1)
endif()

option(HUNTER_ENABLED "Enable Hunter package manager support" ON)
option(HUNTER_STATUS_PRINT "Print working status" ON)
option(HUNTER_STATUS_DEBUG "Print a lot info" OFF)

# If HUNTER_ROOT is defined by user, check it is correct.
# Note: it can't be defined by other Hunter master file
# because of header guards
if(HUNTER_ROOT)
  get_filename_component(HUNTER_ROOT_OLD "${HUNTER_ROOT}" ABSOLUTE)
endif()

# https://github.com/ruslo/hunter/wiki/EP_BASE-layout
if(HUNTER_SHA1)
  # Installed by gate (i.e. HUNTER_ROOT/_Base/SHA1/Self/cmake/Hunter)
  get_filename_component(
      HUNTER_ROOT
      "${CMAKE_CURRENT_LIST_DIR}/../../../.."
      ABSOLUTE
  )
else()
  # Installed manually (i.e. HUNTER_ROOT/cmake/Hunter)
  get_filename_component(HUNTER_ROOT "${CMAKE_CURRENT_LIST_DIR}/.." ABSOLUTE)
endif()

### Directory with hunter sources
get_filename_component(HUNTER_SELF "${CMAKE_CURRENT_LIST_DIR}/.." ABSOLUTE)

### test self
if(NOT EXISTS "${HUNTER_SELF}/cmake/Hunter")
  # Emulate 'hunter_fatal_error'
  message("[hunter ** FATAL ERROR **] Master file not found")
  message("[hunter ** FATAL ERROR **] HUNTER_SELF: `${HUNTER_SELF}`")
  message(
      "[hunter ** FATAL ERROR **] Expected location: <HUNTER_SELF>/cmake/Hunter"
  )
  message("[hunter ** FATAL ERROR **] [Directory:${CMAKE_CURRENT_LIST_DIR}]")
  message("")
  message("------------------------------ WIKI -------------------------------")
  message("    https://github.com/ruslo/hunter/wiki/Error-%28master-file-not-found%29")
  message("-------------------------------------------------------------------")
  message(FATAL_ERROR "")
endif()

### add cmake modules to search path
list(APPEND CMAKE_MODULE_PATH "${HUNTER_SELF}/cmake/modules")
list(APPEND CMAKE_MODULE_PATH "${HUNTER_SELF}/cmake/find")

include(hunter_calculate_config_sha1)
include(hunter_fatal_error)
include(hunter_internal_error)
include(hunter_status_debug)
include(hunter_status_print)

if(HUNTER_ROOT_OLD)
  string(COMPARE NOTEQUAL "${HUNTER_ROOT_OLD}" "${HUNTER_ROOT}" HUNTER_RESULT)
  if(HUNTER_RESULT)
    hunter_internal_error(
        "Incorrent HUNTER_ROOT directories:\n"
        "defined: ${HUNTER_ROOT_OLD}\n"
        "real: ${HUNTER_ROOT}"
    )
  endif()
  unset(HUNTER_RESULT)
endif()

unset(HUNTER_ROOT_OLD)

if(CYGWIN)
  # Make generator has problems with windows paths

  # HUNTER_ROOT
  execute_process(
      COMMAND
      cygpath
      --absolute
      "${HUNTER_ROOT}"
      OUTPUT_VARIABLE
      HUNTER_ROOT
      RESULT_VARIABLE
      _hunter_result
      OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  if(NOT _hunter_result EQUAL 0)
    hunter_internal_error("cygpath failed")
  endif()

  # HUNTER_SELF
  execute_process(
      COMMAND
      cygpath
      --absolute
      "${HUNTER_SELF}"
      OUTPUT_VARIABLE
      HUNTER_SELF
      RESULT_VARIABLE
      _hunter_result
      OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  if(NOT _hunter_result EQUAL 0)
    hunter_internal_error("cygpath failed")
  endif()
endif()

if(NOT HUNTER_CONFIG)
  set(HUNTER_CONFIG "${HUNTER_SELF}/cmake/configs/default.cmake")
endif()

if(NOT EXISTS "${HUNTER_CONFIG}")
  if(HUNTER_GLOBAL)
    file(
        GLOB _hunter_config_variants
        RELATIVE "${HUNTER_SELF}/cmake/configs"
        "${HUNTER_SELF}/cmake/configs/*.cmake"
    )
    message(
         "Config specified by GLOBAL (`${HUNTER_GLOBAL}`) not found. "
         "Available GLOBAL options:\n"
         "-----------------"
    )
    foreach(_hunter_config_variant ${_hunter_config_variants})
      get_filename_component(_hunter_config "${_hunter_config_variant}" NAME_WE)
      message("  * ${_hunter_config}")
    endforeach()
    message("-----------------")
  endif()
  hunter_fatal_error(
       "Config not found: ${HUNTER_CONFIG}"
       WIKI "https://github.com/ruslo/hunter/wiki/Error-%28incorrect-input-data%29"
  )
endif()

if(HUNTER_CONFIG_SHA1)
  hunter_status_debug("Use cached Config-SHA1: ${HUNTER_CONFIG_SHA1}")
else()
  hunter_calculate_config_sha1()
endif()

if(NOT HUNTER_CONFIG_SHA1)
  hunter_internal_error("HUNTER_CONFIG_SHA1 is empty")
else()
  string(SUBSTRING "${HUNTER_CONFIG_SHA1}" 0 7 HUNTER_CONFIG_SHA1_SHORT)
endif()

if(NOT HUNTER_BASE)
  # Check HUNTER_SHA1_SHORT collision
  if(HUNTER_SHA1)
    if(NOT HUNTER_SHA1_SHORT)
      hunter_internal_error("HUNTER_SHA1_SHORT empty")
    endif()
    set(HUNTER_BASE "${HUNTER_ROOT}/_Base/${HUNTER_SHA1_SHORT}")
    if(NOT EXISTS "${HUNTER_ROOT}/_Base/${HUNTER_SHA1_SHORT}/sha1")
      file(
          WRITE
          "${HUNTER_ROOT}/_Base/${HUNTER_SHA1_SHORT}/sha1"
          "${HUNTER_SHA1}"
      )
    else()
      file(
          READ
          "${HUNTER_ROOT}/_Base/${HUNTER_SHA1_SHORT}/sha1"
          _hunter_check_sha1
      )
      if(NOT _hunter_check_sha1 STREQUAL HUNTER_SHA1)
        hunter_internal_error("Short SHA1 collision!")
      endif()
    endif()
  endif()

  # Set HUNTER_BASE
  set(HUNTER_BASE "${HUNTER_ROOT}/_Base/${HUNTER_SHA1_SHORT}")
  set(HUNTER_BASE "${HUNTER_BASE}/${HUNTER_CONFIG_SHA1_SHORT}")

  # Check HUNTER_CONFIG_SHA1_SHORT collision
  if(NOT EXISTS "${HUNTER_BASE}/sha1")
    file(WRITE "${HUNTER_BASE}/sha1" "${HUNTER_CONFIG_SHA1}")
  else()
    file(READ "${HUNTER_BASE}/sha1" _hunter_check_sha1)
    if(NOT _hunter_check_sha1 STREQUAL HUNTER_CONFIG_SHA1)
      hunter_internal_error("Short SHA1 collision (config)!")
    endif()
  endif()
endif()

get_filename_component(HUNTER_BASE "${HUNTER_BASE}" ABSOLUTE)

if(HUNTER_PACKAGE_DOWNLOAD_DIR)
  get_filename_component(
      HUNTER_PACKAGE_DOWNLOAD_DIR "${HUNTER_PACKAGE_DOWNLOAD_DIR}" ABSOLUTE
  )
endif()

set(
    HUNTER_EFFECTIVE_CONFIG
    "${HUNTER_ROOT}/_Base/${HUNTER_SHA1_SHORT}/${HUNTER_CONFIG_SHA1}.cmake"
)
if(NOT EXISTS "${HUNTER_EFFECTIVE_CONFIG}")
  hunter_internal_error("Effective config is not exists")
else()
  get_filename_component(
      HUNTER_EFFECTIVE_CONFIG "${HUNTER_EFFECTIVE_CONFIG}" ABSOLUTE
  )
endif()

hunter_status_print("HUNTER_ROOT: ${HUNTER_ROOT}")
hunter_status_print("HUNTER_BASE: ${HUNTER_BASE}")

hunter_status_debug("HUNTER_SELF: ${HUNTER_SELF}")
if(HUNTER_SHA1)
  hunter_status_debug("HUNTER_SHA1: ${HUNTER_SHA1_SHORT} (${HUNTER_SHA1})")
endif()
hunter_status_debug(
    "HUNTER_CONFIG_SHA1: ${HUNTER_CONFIG_SHA1_SHORT} (${HUNTER_CONFIG_SHA1})"
)
hunter_status_debug("Effective config: ${HUNTER_EFFECTIVE_CONFIG}")


if(HUNTER_PACKAGE_DOWNLOAD_DIR)
  hunter_status_debug(
      "HUNTER_PACKAGE_DOWNLOAD_DIR: ${HUNTER_PACKAGE_DOWNLOAD_DIR}"
  )
endif()

### Check 'EP_BASE` correctness
get_directory_property(HUNTER_EP_BASE_OLD EP_BASE)

if(HUNTER_EP_BASE_OLD)
  hunter_internal_error("EP_BASE directory property already set")
endif()

unset(HUNTER_EP_BASE_OLD)

set_directory_properties(
    PROPERTIES
    EP_BASE
    "${HUNTER_BASE}"
)

if(NOT CMAKE_DEBUG_POSTFIX)
  set(CMAKE_DEBUG_POSTFIX d)
  hunter_status_debug(
      "CMAKE_DEBUG_POSTFIX is empty, set to '${CMAKE_DEBUG_POSTFIX}'"
  )
endif()

if(NOT HUNTER_INSTALL_TAG)
  if(MSVC)
    string(REPLACE "Visual Studio" "vs" HUNTER_INSTALL_TAG "${CMAKE_GENERATOR}")
    string(REPLACE " " "-" HUNTER_INSTALL_TAG "${HUNTER_INSTALL_TAG}")
    string(TOLOWER "${HUNTER_INSTALL_TAG}" HUNTER_INSTALL_TAG)
  elseif(MINGW)
    set(HUNTER_INSTALL_TAG "mingw")
  elseif(CYGWIN)
    set(HUNTER_INSTALL_TAG "cygwin")
  else()
    set(HUNTER_INSTALL_TAG "default")
  endif()
  hunter_status_debug(
      "HUNTER_INSTALL_TAG is empty, set to '${HUNTER_INSTALL_TAG}'"
  )
endif()

if(MSVC)
  include(hunter_setup_msvc_arch)
  include(hunter_setup_msvc_vcvarsall)
  hunter_setup_msvc_arch()
  hunter_setup_msvc_vcvarsall()
endif()

### Cache default cmake generator
include(hunter_set_cmake_default_generator)
hunter_set_cmake_default_generator()

if(HUNTER_ENABLED)
  set(HUNTER_LOCK_PATH "${HUNTER_BASE}/directory-lock")
  include(hunter_verify_toolchain_info)
  hunter_verify_toolchain_info()

  ### 1. Clear all '<NAME>_ROOT' variables (cache, environment, ...)
  ### 2. Set '<NAME>_ROOT' or 'HUNTER_<name>_VERSION' variables
  include("${HUNTER_EFFECTIVE_CONFIG}")
else()
  hunter_status_print("DISABLED")
endif()
